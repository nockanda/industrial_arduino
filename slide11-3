#include <ArduinoJson.h>

unsigned long btn1_t = 0;
unsigned long btn2_t = 0;
bool old_btn1 = LOW;
bool old_btn2 = LOW;

void setup() {
  //Serial이라는게 USB로 PC에 연결된 통신라인을 의미한다!
  Serial.begin(9600);
  pinMode(37,INPUT);//빨강
  pinMode(36,INPUT);//녹색
  //산업용 아두이노의 모든 포트 개방
  for(int i = 38;i<=51;i++){
    pinMode(i,OUTPUT);
  }
}

void loop() {
  //버튼을 눌러서 C#에게 명령을 보내는 부분
  bool now_btn1 = digitalRead(37);
  bool now_btn2 = digitalRead(36);

  if(old_btn1 == LOW && now_btn1 == HIGH){
    //상승엣지
    btn1_t = millis();
  }else if(old_btn1 == HIGH && now_btn1 == LOW){
    //하강엣지
    if(millis() - btn1_t > 50){
       //멈춤 
       Serial.println("{\"run\":\"off\"}");
    }
  }
  if(old_btn2 == LOW && now_btn2 == HIGH){
    //상승엣지
    btn2_t = millis();
  }else if(old_btn2 == HIGH && now_btn2 == LOW){
    //하강엣지
    if(millis() - btn2_t > 50){
      //시작
      Serial.println("{\"run\":\"on\"}");
    }
  }

  old_btn1 = now_btn1;
  old_btn2 = now_btn2;
  //C#에서 제어를 당하는 부분
  if(Serial.available()){
    String data = Serial.readStringUntil('\n'); //JSON데이터가 들어온다!
    data.replace("\r",""); //CR소거
    
    StaticJsonDocument<48> doc;

    DeserializationError error = deserializeJson(doc, data);
    
    if (error) {
      Serial.print(F("deserializeJson() failed: "));
      Serial.println(error.f_str());
      return;
    }
    
    String cmd = doc["cmd"]; // "on"
    int ch = doc["ch"]; // 38

    if(cmd == "on"){
      digitalWrite(ch,HIGH);
    }else if(cmd == "off"){
      digitalWrite(ch,LOW);
    }else if(cmd == "init"){
      //아무것도 할 필요없음!
    }else if(cmd == "all"){
      if(ch == 0){
        //전부다 OFF
        for(int i = 38;i<=51;i++){
          digitalWrite(i,LOW);
        }
      }else if(ch == 1){
        //전부다 ON
        for(int i = 38;i<=51;i++){
          digitalWrite(i,HIGH);
        }
      }
    }

    //제어가 끝난 시점
    //응답데이터 만들기
    //{"response":[0,1,1,1,1,1,1,0,0,0,1,1,1,1]}
    String response = "{\"response\":[";
    for(int i = 38;i<=50;i++){
      response += String(digitalRead(i)) + ",";
    }
    response += String(digitalRead(51)) + "]}";
    Serial.println(response); //산업용 아두이노->PC로 응답하기!
  }
}


