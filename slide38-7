#include <SimpleModbusRtu_mega2560_2.h>//라이브러리 사용하겠다!
#define pump 38 //펌프
#define flow 20 //유량
#define btn_on 36 //녹색
#define btn_off 37 //빨강

volatile float myflow = 0;
float now_myflow = 0;
float target_myflow = 0;

const unsigned int total_count = 10;
bool mysw[] = {false,false,false,false,false,false,false,false,false,false};
unsigned int limit[] = {10,20,30,40,50,60,70,80,90,100};
unsigned int job_num = 0;

//산업용 아두이노에서 메모리를 초과하지 않는 선에서 내가 원하는 만큼 메모리를 할당하면된다!
//unisgned int
uint16_t au16data[16]; //레지스터 주소가(0~15)
//byte
//8bit짜리 자료형이 10개배열로 있으므로 80개의 주소가 있다!
uint8_t au8data[10]; //코일 (0~79)

union{
  uint16_t t[2];
  float f;
}myfloat;
void write_coil(uint8_t * data,uint16_t addr,bool value){
  uint8_t array_num = addr / 8;
  uint8_t array_index = addr % 8;
  bitWrite(data[array_num], array_index, value);
}
bool read_coil(uint8_t * data,uint16_t addr){
  uint8_t array_num = addr / 8;
  uint8_t array_index = addr % 8;
  return bitRead(data[array_num], array_index);
}
void write_float_reg(uint16_t * data,uint16_t addr,float value){
  myfloat.f = value;
  data[addr] = myfloat.t[0];
  data[addr+1]= myfloat.t[1];
}

void setup() {
  //모든 출력포트를 출력으로 설정하겠다!
  for(int i=38; i<=51; i++){
    pinMode(i, OUTPUT);
  }
  //모든 입력포트를 입력으로 설정하겠다!
  for(int i=22; i<=37; i++){
    pinMode(i, INPUT);
  }
   usart3init(9600); //RS232포트중 채널1번에 모드버스 프로토콜을 사용한다!
   //내가 희망하는 슬레이브주소(1), 레지스터(0~15), 코일(0~79)
   startSimpleModbus(1,au16data, au8data); 
   attachInterrupt(digitalPinToInterrupt(flow), nockanda, RISING);
   
}
void loop() {
  //write coil은 0~13
  //출력포트가 14개이기 때문에 반복문이 14회 회전한다!
  for(byte n=0; n<8; n++){
    //38~45번핀까지의 디지털 상태를 설정한다!
    digitalWrite(38+n, bitRead(au8data[0], n));
  }
  for(byte n=0; n<6; n++){
    //46~51번핀까지의 디지털 상태를 설정한다! ..14와 15은 아직 할당 안됨!
    digitalWrite(38+8+n, bitRead(au8data[1], n));
  }

  //read coil은 16~31범위의 주소를 가진다!
  //입력포트가 16개이기 때문에 반복문이 16회 회전한다!
  for(byte n=0; n<8; n++){
    //22~29
    bitWrite( au8data[2],n, digitalRead(n+22) ) ;
  }
  for(byte n=0; n<8; n++){
    //30~37
    bitWrite( au8data[3],n, digitalRead(n+8+22) ) ;
  }

  if(read_coil(au8data,30) == HIGH){
    write_coil(au8data,0,HIGH);
  }
  if(read_coil(au8data,31) == HIGH){
    write_coil(au8data,0,LOW);
  }
  if(read_coil(au8data,32) == HIGH){
    write_coil(au8data,0,HIGH);
    write_coil(au8data,32,LOW);
  }
  if(read_coil(au8data,33) == HIGH){
    write_coil(au8data,0,LOW);
    write_coil(au8data,33,LOW);
    for(int j = 0;j<total_count;j++){
      mysw[j] = false;
    }
  }
  if(read_coil(au8data,34) == HIGH){
    myflow = 0;
    write_coil(au8data,34,LOW);
  }
  for(int i = 0;i<total_count;i++){
    if(read_coil(au8data,35+i) == HIGH){
      now_myflow = 0;
      target_myflow = myflow;
      mysw[i] = true;
      job_num = i+1;
      write_coil(au8data,0,HIGH); //펌프 ON
      write_coil(au8data,35+i,LOW);
    }
    if(mysw[i]){
      //종료지점
      now_myflow = myflow - target_myflow;
      if(now_myflow >= limit[i]){
        mysw[i] = false;
        job_num = 0;
        write_coil(au8data,0,LOW); //펌프 OFF  
      }
    }
  }
  //현재 측정중인 유량을 레지스터에 담는다!
  write_float_reg(au16data,0,myflow);
  write_float_reg(au16data,2,now_myflow);
  au16data[4] = job_num;
}

void nockanda(){
  myflow += (1/5888.0) * 1000;
}


