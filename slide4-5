/*
37번에 연결된 빨간버튼을 누르면 작업이 중지되고
36번에 연결된 녹색버튼을 누르면 작업이 시작된다!
 */

bool task_run = false;
unsigned long t1 = 0; //타이머1
int step1 = 0;
unsigned long t2 = 0; //타이머2
int step2 = 0; 

unsigned long btn1_t = 0;
unsigned long btn2_t = 0;
bool old_btn1 = LOW;
bool old_btn2 = LOW;

void setup() {
  Serial.begin(9600);
  pinMode(38,OUTPUT); //빨간 램프
  pinMode(39,OUTPUT); //녹색 램프
  pinMode(37,INPUT); //빨강버튼
  pinMode(36,INPUT); //녹색버튼

  pinMode(13,OUTPUT);
  digitalWrite(13,LOW);
}
void loop() {
  /////////////작업의 시작과 종료/////////////////
  bool now_btn1 = digitalRead(37);
  bool now_btn2 = digitalRead(36); //녹색

  if(old_btn1 == LOW && now_btn1 == HIGH){
    //rising
    btn1_t = millis();
  }else if(old_btn1 == HIGH && now_btn1 == LOW){
    //falling
    if(millis() - btn1_t > 20){
      //적색 버튼을 눌렀다면~
      task_run = false;
    }
  }
  if(old_btn2 == LOW && now_btn2 == HIGH){
    //rising
    btn2_t = millis();
  }else if(old_btn2 == HIGH && now_btn2 == LOW){
    //falling
    if(millis() - btn2_t > 20){
      //녹색 버튼을 눌렀다면~
      task_run = true;
      step1 = 0;
      step2 = 0;
      t1 = millis();
      t2 = millis();
    }
  }
  old_btn1 = now_btn1;
  old_btn2 = now_btn2;
  //////////////task////////////////
  if(task_run){
    ///////////붕어빵///////////////////
    if(millis() - t1 > 0 && step1 == 0){
      //빵틀을연다
      t1 = millis();
      digitalWrite(38,HIGH);
      Serial.println("붕어빵틀을 연다");
      step1++;
    }else if(millis() - t1 > 1000 && step1 == 1){
      //반죽을 넣는다
      t1 = millis();
      Serial.println("붕어빵 반죽을 넣는다!");
      step1++;
    }else if(millis() - t1 > 1000 && step1 == 2){
      //굽는다
      t1 = millis();
      Serial.println("붕어빵을 굽는다!");
      step1++;
    }else if(millis() - t1 > 3000 && step1 == 3){
      //배출한다
      t1 = millis();
      Serial.println("붕어빵을 배출한다!");
      digitalWrite(38,LOW);
      step1++;
    }else if(millis() - t1 > 1000 && step1 == 4){
      t1 = millis();
      //아무것도 하지 않지만 마지막 시간 
      step1 = 0;
    }
    ///////////잉어빵///////////////////
    if(millis() - t2 > 0 && step2 == 0){
      t2 = millis();
      digitalWrite(39,HIGH);
      Serial.println("\t잉어빵틀을 연다");
      step2++;
    }else if(millis() - t2 > 1000 && step2 == 1){
      t2 = millis();
      Serial.println("\t잉어빵 반죽을 넣는다");
      step2++;
    }else if(millis() - t2 > 1000 && step2 == 2){
      t2 = millis();
      Serial.println("\t잉어빵을 굽는다");
      step2++;
    }else if(millis() - t2 > 4000 && step2 == 3){
      t2 = millis();
      Serial.println("\t잉어빵을 배출한다");
      digitalWrite(39,LOW);
      step2++;
    }else if(millis() - t2 > 1000 && step2 == 4){
      t2 = millis();
      step2 = 0;
    }
  }
}
