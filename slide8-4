//산업용 아두이노의 task가 여러개 있다! task마다 번호가 매겨져있는데 키패드로 해당되는 task번호를 입력하면 해당되는 태스크가 작동되도록 하시오!
//1+#을 누르면 1번작업 시작
//1+*을 누르면 1번작업 종료
#include <Keypad.h>
#include <MsTimer2.h>

const byte ROWS = 4;
const byte COLS = 4;

char hexaKeys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
byte rowPins[ROWS] = {20, 21, 6, 7}; //R1,2,3,4
byte colPins[COLS] = {8, 9, 10, 11}; //C1,2,3,4

Keypad customKeypad = Keypad( makeKeymap(hexaKeys), rowPins, colPins, ROWS, COLS); 

String mynumber = "";

//38번과 39번에 번갈아가면서 깜빡인다(1초간격)
bool task1_run = false;
int task1_counter = 0;
//49,50,51번이 번갈아가면서 깜빡인다(1초간격)
bool task2_run = false;
int task2_counter = 0;

void task(){
  if(task1_run){
    if(task1_counter == 0){
      digitalWrite(38,HIGH);
      digitalWrite(39,LOW);
    }else if(task1_counter == 2){
      digitalWrite(38,LOW);
      digitalWrite(39,HIGH);
    }else if(task1_counter == 4){
      task1_counter = -1; 
    }
    task1_counter++;
  }
  if(task2_run){
    if(task2_counter == 0){
      digitalWrite(49,HIGH);
      digitalWrite(50,LOW);
      digitalWrite(51,LOW);
    }else if(task2_counter == 2){
      digitalWrite(49,LOW);
      digitalWrite(50,HIGH);
      digitalWrite(51,LOW);
    }else if(task2_counter == 4){
      digitalWrite(49,LOW);
      digitalWrite(50,LOW);
      digitalWrite(51,HIGH);
    }else if(task2_counter == 6){
      task2_counter = -1;
    }
    task2_counter++;
  }
}
void setup(){
  Serial.begin(9600);
  //모든 산업용 아두이노의 출력핀을 OUTPUT으로 설정한다!
  //38~51
  for(int i = 38;i<=51;i++){
    pinMode(i,OUTPUT);
  }
  MsTimer2::set(500, task);
  MsTimer2::start();
}
  
void loop(){
  char customKey = customKeypad.getKey();
  
  if (customKey >= '0' && customKey <= '9'){
    //숫자를 전역변수에 저장한다!
    mynumber += customKey;
  }else if(customKey == '#'){
    //커맨드입력
    if(mynumber == "1"){
      task1_counter = 0;
      task1_run = true;
    }else if(mynumber == "2"){
      task2_counter = 0;
      task2_run = true;
    }
    mynumber = "";//커맨드가 들어간다음에는 초기화한다!
  }else if(customKey == '*'){
    if(mynumber == "1"){
      digitalWrite(38,LOW);
      digitalWrite(39,LOW);
      task1_run = false;
    }else if(mynumber == "2"){
      digitalWrite(49,LOW);
      digitalWrite(50,LOW);
      digitalWrite(51,LOW);
      task2_counter = 0;
      task2_run = false;
    }
    mynumber = "";//커맨드가 들어간다음에는 초기화한다!
  }
}

